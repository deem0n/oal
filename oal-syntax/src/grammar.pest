//
// Created by intellij-pest on 2021-10-16
// grammar.pest
// Author: ebastien
//

/// red
ident = @{ !keyword ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

num_kw  = @{ "num"  ~ !ASCII_ALPHANUMERIC }
str_kw  = @{ "str"  ~ !ASCII_ALPHANUMERIC }
uri_kw  = @{ "uri"  ~ !ASCII_ALPHANUMERIC }
bool_kw = @{ "bool" ~ !ASCII_ALPHANUMERIC }
let_kw  = @{ "let"  ~ !ASCII_ALPHANUMERIC }
res_kw  = @{ "res"  ~ !ASCII_ALPHANUMERIC }

get_kw = @{ "get" ~ !ASCII_ALPHANUMERIC }
put_kw = @{ "put" ~ !ASCII_ALPHANUMERIC }

method = { get_kw | put_kw }

/// green
keyword = { num_kw | str_kw | uri_kw | bool_kw | let_kw | res_kw | get_kw | put_kw }

uri_seg = @{ ( ASCII_ALPHANUMERIC | "-" | "." | "_" | "~" | "%" )+ }
uri_lit = { "/" ~ uri_seg }
uri_tpl = { "/{" ~ prop ~ "}" }
uri_spec = { ( uri_tpl | uri_lit )+ }

/// yellow
uri_type = { uri_kw ~ ( ":" ~ uri_spec )? }

methods = { method ~ ( "," ~ method )* }

rel_type = { ( uri_type | ident ) ~ ":" ~ methods ~ "->" ~ sum_type }

prim_type = { num_kw | str_kw | bool_kw  }

prop = { ident ~ sum_type }
block_type = { "{" ~ prop* ~ "}" }
paren_type = { "(" ~ sum_type ~ ")" }
term_type = { block_type | paren_type | ident }
join_type = { term_type ~ ( "&" ~ term_type )* }
comp_type =  { prim_type | rel_type | uri_type | join_type }
sum_type = { comp_type ~ ( "|" ~ comp_type )* }

decl = { let_kw ~ ident ~ "=" ~ sum_type }

res = { res_kw ~ ( rel_type | ident ) }

stmt = { decl | res }

doc = { SOI ~ stmt* ~ EOI }

block_comment = _{ "/*" ~ ( block_comment | !"*/" ~ ANY )* ~ "*/" }
line_comment = _{ "#" ~ ( !NEWLINE ~ ANY )* }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ line_comment }
