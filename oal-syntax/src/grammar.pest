//
// Created by intellij-pest on 2021-10-16
// grammar.pest
// Author: ebastien
//

ident = @{ !keyword ~ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

any_kw  = @{ "any"  ~ !ASCII_ALPHANUMERIC }
obj_kw  = @{ "obj"  ~ !ASCII_ALPHANUMERIC }
rel_kw  = @{ "rel"  ~ !ASCII_ALPHANUMERIC }
num_kw  = @{ "num"  ~ !ASCII_ALPHANUMERIC }
str_kw  = @{ "str"  ~ !ASCII_ALPHANUMERIC }
uri_kw  = @{ "uri"  ~ !ASCII_ALPHANUMERIC }
bool_kw = @{ "bool" ~ !ASCII_ALPHANUMERIC }
let_kw  = @{ "let"  ~ !ASCII_ALPHANUMERIC }
res_kw  = @{ "res"  ~ !ASCII_ALPHANUMERIC }

type_kw = { any_kw | obj_kw | rel_kw | num_kw | str_kw | uri_kw | bool_kw }

get_kw = @{ "get" ~ !ASCII_ALPHANUMERIC }
put_kw = @{ "put" ~ !ASCII_ALPHANUMERIC }
/// TODO: add remaining HTTP methods

method_kw = { get_kw | put_kw }

keyword = { type_kw | method_kw }

var = { ident }
binding = { ident }

uri_seg = @{ ( ASCII_ALPHANUMERIC | "-" | "." | "_" | "~" | "%" )+ }
uri_lit = { "/" ~ uri_seg }
uri_tpl = { "/{" ~ prop ~ "}" }
uri_spec = { ( uri_tpl | uri_lit )+ }

uri_type = { uri_kw | uri_spec }

methods = { method_kw ~ ( "," ~ method_kw )* }

rel_type = { ( uri_type | var ) ~ ":" ~ methods ~ "->" ~ sum_type }

prim_type = { num_kw | str_kw | bool_kw  }

prop = { ident ~ sum_type }
block_type = { "{" ~ prop* ~ "}" }
paren_type = { "(" ~ sum_type ~ ")" }
term_type = { prim_type | rel_type | uri_type | block_type | paren_type | var }
join_type = { term_type ~ ( "&" ~ term_type )* }
any_type = { join_type ~ ( "~" ~ join_type )* }
sum_type = { any_type ~ ( "|" ~ any_type )* }

bindings = { binding* }
hint = { ( ":" ~ type_kw )? }
decl = { let_kw ~ ident ~ bindings ~ hint ~ "=" ~ sum_type }

res = { res_kw ~ ( rel_type | ident ) }

stmt = { decl | res }

doc = { SOI ~ stmt* ~ EOI }

block_comment = _{ "/*" ~ ( block_comment | !"*/" ~ ANY )* ~ "*/" }
line_comment = _{ "#" ~ ( !NEWLINE ~ ANY )* }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ line_comment | block_comment }
